group 'yi.app.com.vertx.hw'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    vertxVersion = "3.5.0"
}

repositories {
    mavenCentral()
}

dependencies {
    compile("io.vertx:vertx-core:${vertxVersion}")
    compile("io.vertx:vertx-web:${vertxVersion}")
    compile("io.vertx:vertx-web-client:${vertxVersion}")
    compileOnly ("io.vertx:vertx-codegen:${vertxVersion}")
    compile("io.vertx:vertx-redis-client:${vertxVersion}")
    compile("io.vertx:vertx-rx-java2:${vertxVersion}")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes "Main-Class": "io.vertx.core.Launcher",
            "Main-Verticle": "yi.app.com.vertx.hw.verticle.MainVerticle"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task getHomeDir  {
    doLast{
        println gradle.gradleHomeDir
    }
}

// compileOnly requires Gradle 2.12+
task annotationProcessing(type: JavaCompile, group: 'build') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
        "-proc:only",
        "-processor", "io.vertx.codegen.CodeGenProcessor",
        "-Acodegen.output=${destinationDir.absolutePath}"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}
